## Exercício 

**1- Retrieving Nodes**

Coloque os comandos utilizado em cada item a seguir:

**Exercise 1.1: Retrieve all nodes from the database.**
> MATCH (n) RETURN n

**Exercise 1.2: Examine the data model for the graph.**
> call db.schema.visualization() 

**Exercise 1.3: Retrieve all Person nodes.**
> MATCH (p:Person) RETURN p

**Exercise 1.4: Retrieve all Movie nodes.**
> MATCH (m:Movie) RETURN m

**2 – Filtering queries using property values**

Coloque os comandos utilizado em cada item a seguir:

**Exercise 2.1: Retrieve all movies that were released in a specific year.**
> MATCH (m:Movie {released:2003}) RETURN m

**Exercise 2.2: View the retrieved results as a table.**
> MATCH (m:Movie {released:2003}) RETURN m

```optional
-Retrieve all movie nodes in the database and view the data as a table. Notice the values for the released property for each node.


-Try querying the graph using different years.
MATCH (m:Movie {released:2006}) RETURN m
```

**Exercise 2.3: Query the database for all property keys.**
> CALL db.propertyKeys

**Exercise 2.4: Retrieve all Movies released in a specific year, returning their titles.**
> MATCH (m:Movie {released: 2006}) RETURN m.title

```optional
-Retrieve all Movie nodes and view them as a table. Observe the properties that Movie nodes have.

Query the database using a different year and also return more property values.
MATCH (m:Movie {released: 2008}) RETURN m.tagline, m.title
```

**Exercise 2.5: Display title, released, and tagline values for every Movie node in the graph.**
> MATCH (m:Movie) RETURN m.title, m.released, m.tagline

**Exercise 2.6: Display more user-friendly headers in the table**
> MATCH (m:Movie) RETURN m.title AS `movie title`, m.released AS released, m.tagline AS tagLine

**3 - Filtering queries using relationships**

Coloque os comandos utilizado em cada item a seguir:

**Exercise 3.1: Display the schema of the database.**
> CALL db.schema.visualization()

**Exercise 3.2: Retrieve all people who wrote the movie Speed Racer.**
> MATCH (p:Person)-[:WROTE]->(:Movie {title: 'Speed Racer'}) RETURN p.name

```optional
-Retrieve all people who have written other movies.
MATCH (p:Person)-[:WROTE]->(m:Movie)
WHERE (p)-[:PRODUCED]->(m) 
RETURN p.name,m.title

-Retrieve people who have acted in a particular movie.
MATCH (p:Person)-[:WROTE]->(m:Movie)
WHERE (p)-[:ACTED_IN]->(m) 
RETURN p.name,m.title

-Retrieve people who have directed a particular movie.
MATCH (p:Person)-[:WROTE]->(m:Movie)
WHERE (p)-[:DIRECTED]->(m) 
RETURN p.name,m.title
```

**Exercise 3.3: Retrieve all movies that are connected to the person, Tom Hanks.** 
> MATCH (m:Movie)<--(:Person {name: 'Tom Hanks'}) RETURN m.title

**Exercise 3.4: Retrieve information about the relationships Tom Hanks had with the set of movies retrieved earlier.**
> MATCH (m:Movie)-[rel]-(:Person {name: 'Tom Hanks'}) RETURN m.title, type(rel)

**Exercise 3.5: Retrieve information about the roles that Tom Hanks acted in.**
> MATCH (m:Movie)-[rel:ACTED_IN]-(:Person {name: 'Tom Hanks'}) RETURN m.title, rel.roles

**4 – Filtering queries using WHERE clause**

Coloque os comandos utilizado em cada item a seguir:

**Exercise 4.1: Retrieve all movies that Tom Cruise acted in.**
> MATCH (a:Person)-[:ACTED_IN]->(m:Movie)
WHERE a.name = 'Tom Cruise'
RETURN m.title as Movie

**Exercise 4.2: Retrieve all people that were born in the 70’s.**
> MATCH (a:Person)
WHERE a.born >= 1970 AND a.born < 1980
RETURN a.name as Name, a.born as `Year Born`

**Exercise 4.3: Retrieve the actors who acted in the movie The Matrix who were born after 1960.**
> MATCH (a:Person)-[:ACTED_IN]->(m:Movie)
WHERE a.born > 1960 AND m.title = 'The Matrix'
RETURN a.name as Name, a.born as `Year Born`

**Exercise 4.4: Retrieve all movies by testing the node label and a property.**
> MATCH (m)
WHERE m:Movie AND m.released = 2000
RETURN m.title

**Exercise 4.5: Retrieve all people that wrote movies by testing the relationship between two nodes.**
> MATCH (a)-[rel]->(m)
WHERE a:Person AND type(rel) = 'WROTE' AND m:Movie
RETURN a.name as Name, m.title as Movie

**Exercise 4.6: Retrieve all people in the graph that do not have a property.**
> MATCH (a:Person)
WHERE NOT exists(a.born)
RETURN a.name as Name

**Exercise 4.7: Retrieve all people related to movies where the relationship has a property.**
> MATCH (a:Person)-[rel]->(m:Movie)
WHERE exists(rel.rating)
RETURN a.name as Name, m.title as Movie, rel.rating as Rating

**Exercise 4.8: Retrieve all actors whose name begins with James.**
> MATCH (a:Person)-[:ACTED_IN]->(:Movie)
WHERE a.name STARTS WITH 'James'
RETURN a.name

**Exercise 4.9: Retrieve all all REVIEW relationships from the graph with filtered results.**
> MATCH (:Person)-[r:REVIEWED]->(m:Movie)
WHERE toLower(r.summary) CONTAINS 'fun'
RETURN  m.title as Movie, r.summary as Review, r.rating as Rating

```optional
-Retrieve all movies in the database that have love in their tagline and return the movie titles
MATCH (m:Movie)
WHERE m.tagline =~ '(?i)^.*love.*$'
RETURN m.title

-Retrieve movies in the database, specifying a regular expression for the content of the tagline.
MATCH (m:Movie)
WHERE m.tagline =~ '(?i)^.*and.*$'
RETURN m
```

**Exercise 4.10: Retrieve all people who have produced a movie, but have not directed a movie.**
> MATCH (a:Person)-[:PRODUCED]->(m:Movie)
WHERE NOT ((a)-[:DIRECTED]->(:Movie))
RETURN a.name, m.title

**Exercise 4.11: Retrieve the movies and their actors where one of the actors also directed the movie.**
> MATCH (a1:Person)-[:ACTED_IN]->(m:Movie)<-[:ACTED_IN]-(a2:Person)
WHERE exists( (a2)-[:DIRECTED]->(m) )
RETURN  a1.name as Actor, a2.name as `Actor/Director`, m.title as Movie

**Exercise 4.12: Retrieve all movies that were released in a set of years.**
> MATCH (m:Movie)
WHERE m.released in [2000, 2004, 2008]
RETURN m.title, m.released

**Exercise 4.13: Retrieve the movies that have an actor’s role that is the name of the movie.**
> MATCH (a:Person)-[r:ACTED_IN]->(m:Movie)
WHERE m.title in r.roles
RETURN  m.title as Movie, a.name as Actor

**5 – Controlling query processing**

Coloque os comandos utilizado em cada item a seguir:

**Exercise 5.1: Retrieve data using multiple MATCH patterns.**
> MATCH (a:Person)-[:ACTED_IN]->(m:Movie)<-[:DIRECTED]-(d:Person),
      (a2:Person)-[:ACTED_IN]->(m)
WHERE a.name = 'Gene Hackman'
RETURN m.title as movie, d.name AS director , a2.name AS `co-actors`

**Exercise 5.2: Retrieve particular nodes that have a relationship.**
> MATCH (p1:Person)-[:FOLLOWS]-(p2:Person)
WHERE p1.name = 'James Thompson'
RETURN p1, p2

**Exercise 5.3: Modify the query to retrieve nodes that are exactly three hops away.**
> MATCH (p1:Person)-[:FOLLOWS*3]-(p2:Person)
WHERE p1.name = 'James Thompson'
RETURN p1, p2

**Exercise 5.4: Modify the query to retrieve nodes that are one and two hops away.**
> MATCH (p1:Person)-[:FOLLOWS*1..2]-(p2:Person)
WHERE p1.name = 'James Thompson'
RETURN p1, p2

**Exercise 5.5: Modify the query to retrieve particular nodes that are connected no matter how many hops are required.**
> MATCH (p1:Person)-[:FOLLOWS*]-(p2:Person)
WHERE p1.name = 'James Thompson'
RETURN p1, p2

**Exercise 5.6: Specify optional data to be retrieved during the query.**
> MATCH (p:Person)
WHERE p.name STARTS WITH 'Tom'
OPTIONAL MATCH (p)-[:DIRECTED]->(m:Movie)
RETURN p.name, m.title

**Exercise 5.7: Retrieve nodes by collecting a list.**
> MATCH (p:Person)-[:ACTED_IN]->(m:Movie)
RETURN p.name as actor, collect(m.title) AS `movie list`

**Exercise 5.9: Retrieve nodes as lists and return data associated with the corresponding lists.**
> MATCH (p:Person)-[:REVIEWED]->(m:Movie)
RETURN m.title as movie, count(p) as numReviews, collect(p.name) as reviewers

**Exercise 5.10: Retrieve nodes and their relationships as lists.**
> MATCH (d:Person)-[:DIRECTED]->(m:Movie)<-[:ACTED_IN]-(a:Person)
RETURN d.name AS director, count(a) AS `number actors` , collect(a.name) AS `actors worked with`

**Exercise 5.11: Retrieve the actors who have acted in exactly five movies.**
> MATCH (a:Person)-[:ACTED_IN]->(m:Movie)
WITH  a, count(a) AS numMovies, collect(m.title) AS movies
WHERE numMovies = 5
RETURN a.name, movies

**Exercise 5.12: Retrieve the movies that have at least 2 directors with other optional data.**
> MATCH (m:Movie)
WITH m, size((:Person)-[:DIRECTED]->(m)) AS directors
WHERE directors >= 2
OPTIONAL MATCH (p:Person)-[:REVIEWED]->(m)
RETURN  m.title, p.name

**6 – Controlling results returned**

Coloque os comandos utilizado em cada item a seguir:

**Exercise 6.1: Execute a query that returns duplicate records.**
> MATCH (a:Person)-[:ACTED_IN]->(m:Movie)
WHERE m.released >= 1990 AND m.released < 2000
RETURN DISTINCT m.released, m.title, collect(a.name)

**Exercise 6.2: Modify the query to eliminate duplication.**
> MATCH (a:Person)-[:ACTED_IN]->(m:Movie)
WHERE m.released >= 1990 AND m.released < 2000
RETURN  m.released, collect(m.title), collect(a.name)

**Exercise 6.3: Modify the query to eliminate more duplication.**
> MATCH (a:Person)-[:ACTED_IN]->(m:Movie)
WHERE m.released >= 1990 AND m.released < 2000
RETURN  m.released, collect(DISTINCT m.title), collect(a.name)

**Exercise 6.4: Sort results returned.**
> MATCH (a:Person)-[:ACTED_IN]->(m:Movie)
WHERE m.released >= 1990 AND m.released < 2000
RETURN  m.released, collect(DISTINCT m.title), collect(a.name)
ORDER BY m.released DESC

**Exercise 6.5: Retrieve the top 5 ratings and their associated movies.**
> MATCH (:Person)-[r:REVIEWED]->(m:Movie)
RETURN  m.title AS movie, r.rating AS rating
ORDER BY r.rating DESC LIMIT 5

**Exercise 6.6: Retrieve all actors that have not appeared in more than 3 movies.**
> MATCH (a:Person)-[:ACTED_IN]->(m:Movie)
WITH  a,  count(a) AS numMovies, collect(m.title) AS movies
WHERE numMovies <= 3
RETURN a.name, movies

**7 – Working with cypher data**

Coloque os comandos utilizado em cada item a seguir:

**Exercise 7.1: Collect and use lists.**
> MATCH (a:Person)-[:ACTED_IN]->(m:Movie),
      (m)<-[:PRODUCED]-(p:Person)
WITH  m, collect(DISTINCT a.name) AS cast, collect(DISTINCT p.name) AS producers
RETURN DISTINCT m.title, cast, producers
ORDER BY size(cast)

**Exercise 7.2: Collect a list.**
> MATCH (p:Person)-[:ACTED_IN]->(m:Movie)
WITH p, collect(m) AS movies
WHERE size(movies)  > 5
RETURN p.name, movies

**Exercise 7.3: Unwind a list.**
> MATCH (p:Person)-[:ACTED_IN]->(m:Movie)
WITH p, collect(m) AS movies
WHERE size(movies)  > 5
WITH p, movies UNWIND movies AS movie
RETURN p.name, movie.title

**Exercise 7.4: Perform a calculation with the date type.**
> MATCH (a:Person)-[:ACTED_IN]->(m:Movie)
WHERE a.name = 'Tom Hanks'
RETURN  m.title, m.released, date().year  - m.released as yearsAgoReleased, m.released  - a.born AS `age of Tom`
ORDER BY yearsAgoReleased

**8 – Creating nodes**

Coloque os comandos utilizado em cada item a seguir:

**Exercise 8.1: Create a Movie node.**
> CREATE (:Movie {title: 'Forrest Gump'})

**Exercise 8.2: Retrieve the newly-created node.**
> MATCH (m:Movie)
WHERE m.title = 'Forrest Gump'
RETURN m

**Exercise 8.3: Create a Person node.**
> CREATE (:Person {name: 'Robin Wright'})

**Exercise 8.4: Retrieve the newly-created node.**
> MATCH (p:Person)
WHERE p.name = 'Robin Wright'
RETURN p

**Exercise 8.5: Add a label to a node.**
> MATCH (m:Movie)
WHERE m.released < 2010
SET m:OlderMovie
RETURN DISTINCT labels(m)

**Exercise 8.6: Retrieve the node using the new label.**
> MATCH (m:OlderMovie)
RETURN m.title, m.released

**Exercise 8.7: Add the Female label to selected nodes.**
> MATCH (p:Person)
WHERE p.name STARTS WITH 'Robin'
SET p:Female

**Exercise 8.8: Retrieve all Female nodes.**
> MATCH (p:Female)
RETURN p.name

**Exercise 8.9: Remove the Female label from the nodes that have this label.**
> MATCH (p:Female)
REMOVE p:Female

Removed 2 labels, completed after 2 ms

**Exercise 8.10: View the current schema of the graph.**
> CALL db.schema.visualization

**Exercise 8.11: Add properties to a movie.**
> MATCH (m:Movie)
WHERE m.title = 'Forrest Gump'
SET m:OlderMovie,
    m.released = 1994,
    m.tagline = "Life is like a box of chocolates...you never know what you're gonna get.",
    m.lengthInMinutes = 142
    
Added 1 label, set 3 properties, completed after 3 ms.

**Exercise 8.12: Retrieve an OlderMovie node to confirm the label and properties.**
> MATCH (m:OlderMovie)
WHERE m.title = 'Forrest Gump'
RETURN m

**Exercise 8.13: Add properties to the person, Robin Wright.**
> MATCH (p:Person)
WHERE p.name = 'Robin Wright'
SET p.born = 1966, p.birthPlace = 'Dallas'

**Exercise 8.14: Retrieve an updated Person node.**
> MATCH (p:Person)
WHERE p.name = 'Robin Wright'
RETURN p

**Exercise 8.15: Remove a property from a Movie node.**
> MATCH (m:Movie)
WHERE m.title = 'Forrest Gump'
SET m.lengthInMinutes = null

Set 1 property, completed after 7 ms.

**Exercise 8.16: Retrieve the node to confirm that the property has been removed.**
> MATCH (m:Movie)
WHERE m.title = 'Forrest Gump'
RETURN m

**Exercise 8.17: Remove a property from a Person node.**
> MATCH (p:Person)
WHERE p.name = 'Robin Wright'
REMOVE p.birthPlace

**Exercise 8.18: Retrieve the node to confirm that the property has been removed.**
> MATCH (p:Person)
WHERE p.name = 'Robin Wright'
RETURN p

``` optional
Add more labels to the Movie nodes to reflect the movie genre (action, drama, etc.).
MATCH (m:Movie)
WHERE toLower(m.title) =~ '(?i)^.*matrix.*$'
and not EXISTS (m.genre)
SET m.genre  = 'Action'

MATCH (m:Movie)
WHERE toLower(m.tagline) =~ '(?i)^.*love.*$'
and not EXISTS (m.genre)
SET m.genre  = 'Romance'

MATCH (m:Movie)
WHERE not EXISTS (m.genre)
SET m.genre  = 'Other'

Query the database using different labels for movies.
MATCH (m:Movie)
WHERE m.genre  = 'Action'
return m

Try adding or updating properties using the JSON-style syntax using = and +=.
MATCH (m:Movie { genre: 'Other' })
SET m += { label: 'Undefined' }
RETURN m

MATCH (m:Movie { genre: 'Other' })
SET m = { label: 'Other' }
RETURN m

Add properties to nodes using the JSON-style format where you add all of the properties to the node.
MATCH (m:Movie { label: 'Other' })
SET m = {genre: "Action",title: "Teste", label: "Other"}
RETURN m

Query the database to confirm your additions.
MATCH (m:Movie { genre: 'Action' }) return m

Call the Cypher built-in method to retrieve all of the property keys in the graph.
CALL db.propertyKeys
```

**9 – Creating relationships**

Coloque os comandos utilizado em cada item a seguir:

**Exercise 9.1: Create ACTED_IN relationships.**
> MATCH (m:Movie)
WHERE m.title = 'Forrest Gump'
MATCH (p:Person)
WHERE p.name = 'Tom Hanks' OR p.name = 'Robin Wright' OR p.name = 'Gary Sinise'
CREATE (p)-[:ACTED_IN]->(m)

**Exercise 9.2: Create DIRECTED relationships.**
> MATCH (m:Movie)
WHERE m.title = 'Forrest Gump'
MATCH (p:Person)
WHERE p.name = 'Robert Zemeckis'
CREATE (p)-[:DIRECTED]->(m)

**Exercise 9.3: Create a HELPED relationship.**
> MATCH (p1:Person)
WHERE p1.name = 'Tom Hanks'
MATCH (p2:Person)
WHERE p2.name = 'Gary Sinise'
CREATE (p1)-[:HELPED]->(p2)

**Exercise 9.4: Query nodes and new relationships.**
> MATCH (p:Person)-[rel]-(m:Movie)
WHERE m.title = 'Forrest Gump'
RETURN p, rel, m

**Exercise 9.5: Add properties to relationships.**
> MATCH (p:Person)-[rel:ACTED_IN]->(m:Movie)
WHERE m.title = 'Forrest Gump'
SET rel.roles =
CASE p.name
  WHEN 'Tom Hanks' THEN ['Forrest Gump']
  WHEN 'Robin Wright' THEN ['Jenny Curran']
  WHEN 'Gary Sinise' THEN ['Lieutenant Dan Taylor']
END

Set 3 properties, completed after 41 ms.

**Exercise 9.6: Add a property to the HELPED relationship.**
> MATCH (p1:Person)-[rel:HELPED]->(p2:Person)
WHERE p1.name = 'Tom Hanks' AND p2.name = 'Gary Sinise'
SET rel.research = 'war history'

Set 1 property, completed after 544 ms.

**Exercise 9.7: View the current list of property keys in the graph.**
> call db.propertyKeys

**Exercise 9.8: View the current schema of the graph.**
> call db.schema.visualization

**Exercise 9.9: Retrieve the names and roles for actors.**
> MATCH (p:Person)-[rel:ACTED_IN]->(m:Movie)
WHERE m.title = 'Forrest Gump'
RETURN p.name, rel.roles

**Exercise 9.10: Retrieve information about any specific relationships.**
> MATCH (p1:Person)-[rel:HELPED]-(p2:Person)
RETURN p1.name, rel, p2.name

**Exercise 9.11: Modify a property of a relationship.**
> MATCH (p:Person)-[rel:ACTED_IN]->(m:Movie)
WHERE m.title = 'Forrest Gump' AND p.name = 'Gary Sinise'
SET rel.roles =['Lt. Dan Taylor']

Set 1 property, completed after 3 ms.

**Exercise 9.12: Remove a property from a relationship.**
> MATCH (p1:Person)-[rel:HELPED]->(p2:Person)
WHERE p1.name = 'Tom Hanks' AND p2.name = 'Gary Sinise'
REMOVE rel.research

Set 1 property, completed after 4 ms.

**Exercise 9.13: Confirm that your modifications were made to the graph.**
> MATCH (p:Person)-[rel:ACTED_IN]->(m:Movie)
WHERE m.title = 'Forrest Gump'
return p, rel, m

**10 – Deleting nodes and relationships**

Coloque os comandos utilizado em cada item a seguir:

**Exercise 10.1: Delete a relationship.**
> MATCH (:Person)-[rel:HELPED]-(:Person)
DELETE rel

Deleted 1 relationship, completed after 118 ms.

**Exercise 10.2: Confirm that the relationship has been deleted.**
> MATCH (:Person)-[rel:HELPED]-(:Person)
RETURN rel

**Exercise 10.3: Retrieve a movie and all of its relationships.**
> MATCH (p:Person)-[rel]-(m:Movie)
WHERE m.title = 'Forrest Gump'
RETURN p, rel, m

**Exercise 10.4: Try deleting a node without detaching its relationships.**
> MATCH (m:Movie)
WHERE m.title = 'Forrest Gump'
DELETE m

**Exercise 10.5: Delete a Movie node, along with its relationships.**
> MATCH (m:Movie)
WHERE m.title = 'Forrest Gump'
DETACH DELETE m

Deleted 1 node, deleted 4 relationships, completed after 3 ms.

**Exercise 10.6: Confirm that the Movie node has been deleted.**
> MATCH (p:Person)-[rel]-(m:Movie)
WHERE m.title = 'Forrest Gump'
RETURN p, rel, m
